apply plugin: 'android'

repositories {
    mavenCentral()
}
dependencies {
    compile 'com.android.support:support-v4:18.0.+'
    compile project(':lib')
}

android {
    compileSdkVersion 18
    buildToolsVersion "18.1.1"
    compileOptions.encoding "UTF-8"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    defaultConfig {
        packageName project.DEMO
        minSdkVersion 8
        targetSdkVersion 18
        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)
    }

    signingConfigs {
        release
    }

    buildTypes {
        debug {
            packageNameSuffix ".debug"
            versionNameSuffix ".d"
            debuggable true
        }
        release {
            zipAlign true

            runProguard true
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard.cfg'
            signingConfig signingConfigs.release
        }
    }
}

if (project.hasProperty('releaseFile') &&
        project.hasProperty('releasePassword') &&
        project.hasProperty('releaseAlias') &&
        project.hasProperty('releaseAliasPassword')) {
    android.signingConfigs.release.storeFile = file(releaseFile)
    android.signingConfigs.release.storePassword = releasePassword
    android.signingConfigs.release.keyAlias = releaseAlias
    android.signingConfigs.release.keyPassword = releaseAliasPassword
}
else {
    println 'Release key information not found.'
    buildTypes.release.signingConfig = null
}
